
--- Basic stuff.

comment "//" ;

-- Give ID tokens line/column info.
position token ID      (letter (letter | digit)*)     ;
position token Keyword (letter (letter | digit)* ':') ;
position token Op      ('~' | '!' | '@' | '%' | '&'  | '*' |
	                '-' | '+' | '=' | '|' | '\\' | '<' |
                        '>' | ',' | '?' | '/')+ ;

--- Top level.

ClassFile.      ClassFile       ::=  "class" ID "." [TopLevelClause]     ;
StaticClause.   TopLevelClause  ::=  "static"          "{" [VarDecl] "}" ;
MembersClause.  TopLevelClause  ::=  "members"         "{" [VarDecl] "}" ;
MethodClause.   TopLevelClause  ::=  Type DeclSelector "{" [Stmt]    "}" ;

separator TopLevelClause "" ;
separator VarDecl        "" ;
separator Stmt           "" ;
separator DeclKeyword    "" ;

VarDecl.      VarDecl       ::=  "var" ID VarDeclTail "." ;

UntypedInit.  VarDeclTail   ::=              Initializer ;
TypedInit.    VarDeclTail   ::= "(" Type ")" Initializer ;

Initializer.   Initializer  ::= ":=" Expr ;
NoInitializer. Initializer  ::= ;

DSelUnary.    DeclSelector  ::=  ID            ;
DSelBinary.   DeclSelector  ::=  Op IDWithType ;
DSelKeyword.  DeclSelector  ::=  [DeclKeyword] ;

DeclKeyword.  DeclKeyword   ::=  Keyword IDWithType ;

IDWithType.   IDWithType    ::=  ID "(" Type ")" ;

Type.         Type          ::=  ID ;


--- Statements.

SExpr.  Stmt  ::=  Expr "." ;

--- Expressions.

internal TExpr.  Expr   ::=  Expr3 "::" Type                    ;
ESet.            Expr   ::=  ID ":=" Expr                       ;
EKeywordCall.    Expr   ::=  Expr1 [EKeywordArg]                ;
EBinaryCall.     Expr1  ::=  Expr2 Op Expr                      ;
EUnaryCall.      Expr2  ::=  Expr2 ID                           ;
EVar.            Expr3  ::=  ID                                 ;
EInt.            Expr3  ::=  Integer                            ;
EDouble.         Expr3  ::=  Double                             ;
EVector.         Expr3  ::=  "[" Expr1 "," Expr1 "," Expr1 "]"  ;

separator nonempty EKeywordArg  ""                 ;
EKeywordArg.       EKeywordArg  ::=  Keyword Expr2 ;

coercions Expr 3 ;
